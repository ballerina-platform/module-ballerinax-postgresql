import org.apache.tools.ant.taskdefs.condition.Os

/*
 * Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'java'

description = 'Ballerina - Postgresql Examples'

def ballerinaDist = "${project.rootDir}/target/ballerina-runtime"
def examples = ["atomic-batch-operation", "batch-operation", "call-stored-procedures", "complex-queries-operation", "execute-operation", "query-operation"]

clean {
    examples.forEach { example ->
        delete "${projectDir}/${example}/target"
    }
}

task updateTomlFiles {
    doLast {
        examples.each { example ->
            def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/ExamplesBallerina.toml")
            def ballerinaTomlFile = new File("${project.projectDir}/${example}/Ballerina.toml")
            def newConfig = ballerinaTomlFilePlaceHolder.text.replace('@example.name@', example.replace("-", "_"))
            ballerinaTomlFile.text = newConfig
        }
    }
}

task testExamples {
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir "${project.projectDir}/${example}"
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat test --offline && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal test --offline"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

task buildExamples {
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":java.jdbc-examples:test")) {
            buildExamples.enabled = false
        }
    }
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir "${project.projectDir}/${example}"
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat pack --offline && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal pack --offline"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

buildExamples.dependsOn ":postgresql-ballerina:build", updateTomlFiles
testExamples.dependsOn ":postgresql-ballerina:build", updateTomlFiles
test.dependsOn testExamples
build.dependsOn buildExamples
